plugins {
  id 'java'
  id 'application'
}

group = 'com.unimozer'
version = '0.6.1'

java {
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(JavaCompile).configureEach {
  options.compilerArgs << "-Xlint:unchecked"
}

repositories {
  mavenCentral()
}

dependencies {
  implementation 'com.github.javaparser:javaparser-core:3.28.0'
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.21.0'
}

application {
  mainClass = 'com.unimozer.parser.ParserBridge'
}

jar {
  archiveFileName = 'parser-bridge.jar'
  manifest {
    attributes 'Main-Class': application.mainClass
  }
  from {
    configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  }
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.register('copyBridgeJar', Copy) {
  dependsOn jar
  from "$buildDir/libs/parser-bridge.jar"
  into "${rootProject.projectDir}/../resources/java-parser"
}
